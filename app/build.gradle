apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    secrets.file rootProject.file('secrets/secrets.properties')
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.caplaninnovations.looprwallet"
        multiDexEnabled true
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.caplaninnovations.looprwallet.application.LooprTestRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            buildConfigField "Long", "DEFAULT_READ_TIMEOUT", "2000L"
        }
        staging {
            buildConfigField "Long", "DEFAULT_READ_TIMEOUT", "15000L"

            debuggable false
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField "Long", "DEFAULT_READ_TIMEOUT", "15000L"

            debuggable false
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    buildToolsVersion '27.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    flavorDimensions "environment"

    productFlavors {
        mocknet {
            buildConfigProperty "ETHEREUM_URL", buildProperties.secrets["mocknetServerUrl"]

            buildConfigField "Long", "SECURITY_LOCKOUT_TIME", "1000L * 10L"
            buildConfigField "String", "ENVIRONMENT", "\"mocknet\""

            dimension "environment"
            applicationIdSuffix ".mocknet"
            versionNameSuffix "-mocknet"

            // Only include English packages and xxhdpi resources to speed up build times
            resConfigs("en", "xxhdpi")
        }

        // Flavor name cannot start with "test"
        infuraTestnet {
            buildConfigProperty "ETHEREUM_URL", buildProperties.secrets["testnetServerUrl"]

            buildConfigField "Long", "SECURITY_LOCKOUT_TIME", "1000L * 60L * 5L"
            buildConfigField "String", "ENVIRONMENT", "\"testnet\""

            dimension "environment"
            applicationIdSuffix ".infuraTestnet"
            versionNameSuffix "-testnet"
        }

        mainnet {
            buildConfigProperty "ETHEREUM_URL", buildProperties.secrets["mainnetServerUrl"]

            buildConfigField "Long", "SECURITY_LOCKOUT_TIME", "1000L * 60L * 5L"
            buildConfigField "String", "ENVIRONMENT", "\"mainnet\""

            dimension "environment"
            applicationIdSuffix ".mainnet"
            versionNameSuffix "-mainnet"
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    if (project.hasProperty("mockNet")) {
        apptOptions.cruncherEnabled = false
    }
}

realm {
    kotlinExtensionsEnabled = true
}

kapt {
    useBuildCache = true
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android Multidex for apps with more than 65,536 methods
    implementation 'com.android.support:multidex:1.0.3'

    // Android Native Libraries
    ext.androidNativeVersion = '27.1.0'
    implementation "com.android.support:support-v4:$androidNativeVersion"
    implementation "com.android.support:appcompat-v7:$androidNativeVersion"
    implementation "com.android.support:cardview-v7:$androidNativeVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:customtabs:$androidNativeVersion"
    implementation "com.android.support:design:$androidNativeVersion"
    implementation "com.android.support:recyclerview-v7:$androidNativeVersion"
    implementation "com.android.support:support-vector-drawable:$androidNativeVersion"
    implementation "com.android.support:preference-v14:$androidNativeVersion"

    // Architecture Components
    ext.androidArchitectureComponentsVesrion = '1.1.1'
    implementation "android.arch.lifecycle:extensions:$androidArchitectureComponentsVesrion"
    implementation "android.arch.lifecycle:common-java8:$androidArchitectureComponentsVesrion"

    // Blockchain Dependencies
    implementation 'org.web3j:core:3.3.1-android'

    // Dagger Dependency Injection
    ext.daggerVersion = '2.13'
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Firebase
    ext.googleLibraryVersion = '12.0.0'
    implementation "com.google.firebase:firebase-core:$googleLibraryVersion"
    implementation "com.google.firebase:firebase-appindexing:$googleLibraryVersion"
    implementation "com.google.firebase:firebase-crash:$googleLibraryVersion"
    implementation "com.google.firebase:firebase-invites:$googleLibraryVersion"
    implementation "com.google.firebase:firebase-perf:$googleLibraryVersion"

    // Glide Dependencies
    ext.glideVersion = '4.6.1'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.3"
    implementation "androidx.core:core-ktx:0.2"

    // Networking
    ext.retrofitVersion = '2.4.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-experimental-adapter:1.0.0"

    // Other
    implementation "com.google.code.gson:gson:2.8.2"

    // QR Code Scanner
    implementation "com.google.android.gms:play-services-vision:$googleLibraryVersion"

    // Realm
    implementation "io.realm:android-adapters:2.1.1"
    implementation "io.realm:secure-userstore:1.0.1"

    /*
     * TESTING DEPENDENCIES
     */

    // Unit Testing
    testImplementation "junit:junit:4.12"
    testImplementation "android.arch.core:core-testing:$androidArchitectureComponentsVesrion"
    testImplementation "org.mockito:mockito-core:2.7.6"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"

    // Android Testing and Integration Tests
    ext.espressoVersion = '3.0.1'
    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation "org.mockito:mockito-core:2.7.6"
    androidTestImplementation "com.android.support.test:rules:1.0.2-alpha1"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
}
repositories {
    mavenCentral()
}

apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'