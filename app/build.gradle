apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    secrets.file rootProject.file('secrets/secrets.properties')
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.caplaninnovations.looprwallet"
        multiDexEnabled true
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {}
        staging {
            debuggable false
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildToolsVersion '27.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    flavorDimensions "environment"

    dexOptions {
        javaMaxHeapSize "2g"
    }

    productFlavors {

        mocknet {
            buildConfigProperty "ETHEREUM_URL", buildProperties.secrets["mocknetServerUrl"]

            buildConfigField "Long", "SECURITY_LOCKOUT_TIME", "1000L * 10L"
            buildConfigField "String", "ENVIRONMENT", "\"mocknet\""

            applicationIdSuffix ".mocknet"
            versionNameSuffix "-mocknet"

            dimension "environment"
        }

        infuraTestnet {
            buildConfigProperty "ETHEREUM_URL", buildProperties.secrets["testnetServerUrl"]

            buildConfigField "Long", "SECURITY_LOCKOUT_TIME", "1000L * 60L * 5L"
            buildConfigField "String", "ENVIRONMENT", "\"testnet\""

            applicationIdSuffix ".infuraTestnet"
            versionNameSuffix "-testnet"

            dimension "environment"
        }

        mainnet {
            buildConfigProperty "ETHEREUM_URL", buildProperties.secrets["mainnetServerUrl"]

            buildConfigField "Long", "SECURITY_LOCKOUT_TIME", "1000L * 60L * 5L"
            buildConfigField "String", "ENVIRONMENT", "\"mainnet\""

            applicationIdSuffix ".mainnet"
            versionNameSuffix "-mainnet"

            dimension "environment"
        }
    }
}

dependencies {
    implementation project(':library.core')
    implementation project(':module.barcode')
    implementation project(':module.contacts')
    implementation project(':module.home')
    implementation project(':module.intro')
    implementation project(':module.order')
    implementation project(':module.trade-details')
    implementation project(':module.transfer')
    implementation project(':module.wallet-sign-in')

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android Multidex for apps with more than 65,536 methods
    implementation 'com.android.support:multidex:1.0.3'

    // Dagger Dependency Injection
    implementation gradle.dagger
    kapt gradle.daggerProcessor
    kapt gradle.daggerCompiler
}

repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'